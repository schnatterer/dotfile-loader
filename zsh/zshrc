function benchSource() {
    if [[ -n "${BENCH}" ]]; then
        local start=$(date +%s.%N)
        source "$1"
        local end=$(date +%s.%N)
        local runtime=$((end-start))
        
        echo "${runtime}: $1" 
    else
        source "$1"
    fi
}

function debug() {
    [[ -n "${DEBUG}" ]] && echo DEBUG "$@"
}

[[ -n "${TRACE}" ]] && set -x
[[ -n "${BENCH}" ]] && START_TIME=$(date +%s.%N) && echo "Benchmarking loading times in seconds"

# shortcut to this dotfiles path is $ZSH
export DOTFILES_ROOT=$HOME/.dotfiles

if [[ -n  "${PROFILE}" ]]; then
    # See https://kevin.burke.dev/kevin/profiling-zsh-startup-time/ 
    local startupLog=/tmp/startlog.$$
    echo "Profiling to ${startupLog}"
    # http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
    PS4=$'%D{%M%S%.} %N:%i> '
    exec 3>&2 2> $startupLog
    setopt xtrace prompt_subst
    
    # For other alternatives to tracing see: 
    # https://stackoverflow.com/questions/5014823/how-to-profile-a-bash-shell-script-slow-startup    
fi

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  debug "sourcing ~/.localrc"
  benchSource ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES_ROOT/**/*.zsh)

debug "load the path files"
for file in ${(M)config_files:#*/path.zsh}
do
  debug "  sourcing $file"
  benchSource $file
done

debug load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  debug "  sourcing $file"
  benchSource $file
done

debug add each topic folder to fpath so that they can add functions and completion scripts
for topic_folder ($DOTFILES_ROOT/*)
do 
  if [ -d $topic_folder ]; then
    debug "Adding to fpath $topic_folder"
    fpath=($topic_folder $fpath); 
  fi;
done

fpath=($DOTFILES_ROOT/functions $fpath)

debug loading functions from $DOTFILES_ROOT/functions
[[ -n "$(ls $DOTFILES_ROOT/functions/ 2>/dev/null)" ]] && autoload -U $DOTFILES_ROOT/functions/*(:t)

debug "initialize autocomplete here, otherwise functions won't be loaded"
autoload -U compinit
compinit

debug "load every completion after autocomplete loads"
for file in ${(M)config_files:#*/completion.zsh}
do
  debug "  sourcing $file"
  benchSource $file
done

unset config_files

export PATH="$DOTFILES_ROOT/bin:$PATH"

if [[ -n  "${PROFILE}" ]]; then
    unsetopt xtrace
    exec 2>&3 3>&-
fi

[[ -n "${BENCH}" ]] && echo "Startup took: $(($(date +%s.%N)-START_TIME))"
[[ -n "${TRACE}" ]] && set +x