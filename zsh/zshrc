function debug() {
    [[ -n "${DEBUG}" ]] && echo DEBUG "$@"
}

if [[ -n "${TRACE}" ]]; then set -x; fi

# shortcut to this dotfiles path is $ZSH
export DOTFILES_ROOT=$HOME/.dotfiles

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  debug "source ~/.localrc"
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES_ROOT/**/*.zsh)

debug "load the path files"
for file in ${(M)config_files:#*/path.zsh}
do
  debug "  source $file"
  source $file
done

debug load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  debug "  source $file"
  source $file
done

debug add each topic folder to fpath so that they can add functions and completion scripts
for topic_folder ($DOTFILES_ROOT/*)
do 
  if [ -d $topic_folder ]; then
    debug "Adding to fpath $topic_folder"
    fpath=($topic_folder $fpath); 
  fi;
done

fpath=($DOTFILES_ROOT/functions $fpath)

debug loading functions from $DOTFILES_ROOT/functions
[[ -n "$(ls $DOTFILES_ROOT/functions/ 2>/dev/null)" ]] && autoload -U $DOTFILES_ROOT/functions/*(:t)

debug "initialize autocomplete here, otherwise functions won't be loaded"
autoload -U compinit
compinit

debug "load every completion after autocomplete loads"
for file in ${(M)config_files:#*/completion.zsh}
do
  debug "  source $file"
  source $file
done

unset config_files

export PATH="$DOTFILES_ROOT/bin:$PATH"